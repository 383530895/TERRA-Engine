// windows port
Unit TERRA_OS;

{$I terra.inc}

Interface
Uses TERRA_Utils, TERRA_Application, TERRA_Client, TERRA_Multimedia, Windows, Messages;

Const
	PathSeparator = '\';
	CrLf = #13#10;

	keyBackspace  = 8;
	keyTab        = 9;
	keyEnter      = 13;
	keyShift      = 16;
	keyControl    = 17;
	keyAlt        = 18;
	keyPause      = 19;
	keyEscape     = 27;
	keySpace      = 32;
	keyPageUp     = 33;
	keyPageDown   = 34;
	keyEnd        = 35;
	keyHome       = 36;
  keyPlus       = 107;
  keyMinus      = 109;
  keyPeriod     = 190;

	keyLeft       = 37;
	keyUp         = 38;
	keyRight      = 39;
	keyDown       = 40;

	keyInsert     = 45;
	keyDelete     = 46;
	keyF1         = 112;
	keyF2         = 113;
	keyF3         = 114;
	keyF4         = 115;
	keyF5         = 116;
	keyF6         = 117;
	keyF7         = 118;
	keyF8         = 119;
	keyF9         = 120;
	keyF10        = 121;
	keyF11        = 122;
	keyF12        = 123;

  keyA = Ord('A');
  keyB = Ord('B');
  keyC = Ord('C');
  keyD = Ord('D');
  keyE = Ord('E');
  keyF = Ord('F');
  keyG = Ord('G');
  keyH = Ord('H');
  keyI = Ord('I');
  keyJ = Ord('J');
  keyK = Ord('K');
  keyL = Ord('L');
  keyM = Ord('M');
  keyN = Ord('N');
  keyO = Ord('O');
  keyP = Ord('P');
  keyQ = Ord('Q');
  keyR = Ord('R');
  keyS = Ord('S');
  keyT = Ord('T');
  keyU = Ord('U');
  keyV = Ord('V');
  keyW = Ord('W');
  keyX = Ord('X');
  keyY = Ord('Y');
  keyZ = Ord('Z');

Procedure DisplayMessage(S:AnsiString);
Function GetCurrentTime:TERRATime;
Function GetCurrentDate:TERRADate;
Function GetTime:Cardinal;
Function CreateApplicationClass(Client:AppClient):Application;

Procedure SetWindowTitle(Handle:Cardinal; Name:AnsiString);

Type
  WindowsGamepad = Record
    LocalID:Integer;
    DeviceID:Integer;
    XInput:Boolean;
  End;

  WindowsApplication = Class(Application)
    Protected
			_HDC:HDC;           // HDC of window
			_hRC:HGLRC;         // OpenGL rendering context
      _PixelFormat:Cardinal;
      _MultisampleFormat:Cardinal;
      _MultiSampleInitialized:Boolean;
      _savedExStyle:Cardinal;
      _savedStyle:Cardinal;
      _rcSaved:TRect;
      _PosX:Integer;
      _PosY:Integer;
      _BorderWidth:Integer;
      _BorderHeight:Integer;
      _CursorVisible:Boolean;

      _OriginalWidth:Integer;
      _OriginalHeight:Integer;

      _FullscreenWidth:Integer;
      _FullscreenHeight:Integer;

			_JoystickCount:Integer;
			_JoyCaps:GamepadCaps;
			_NextJoystickUpdate:Cardinal;

      _Gamepads:Array Of WindowsGamepad;
      _GamepadCount:Integer;

      Function AddGamePad(DeviceID:Integer; XInput:Boolean):Boolean;

      Function InitWindow:Boolean; Override;
      Function InitGraphics:Boolean; Override;
      Procedure CloseGraphics; Override;
      Procedure CloseWindow; Override;
      Procedure ProcessMessages; Override;

      Function GetClipboard():AnsiString;

      Function GetDocumentPath():AnsiString;

    Public
      Procedure ToggleFullscreen; Override;
      Procedure SwapBuffers; Override;
      Procedure SetState(State:Cardinal); Override;
      Procedure Yeld; Override;

      Function SetOrientation(Value:Integer):Boolean; Override;

      Function GetControllerCount:Integer; Override;

      Procedure OpenAppStore(URL:AnsiString); Override;

      Procedure EnableAds(); Override;
      Procedure DisableAds(); Override;


      Procedure SendEmail(DestEmail, Subject, Body:AnsiString); Override;

      Function IsDebuggerPresent:Boolean; Override;

      Function GetDeviceID():AnsiString; Override;
  End;

Implementation
Uses SysUtils, {$IFDEF GLDEBUG}TERRA_DebugGL{$ELSE}TERRA_GL{$ENDIF},
  TERRA_Renderer, TERRA_Log, TERRA_XInput, TERRA_MusicManager, TERRA_NetBios;

Function ChangeDisplaySettings(lpDevMode: PDeviceMode; dwFlags: DWORD): Longint; stdcall; external 'user32.dll' name 'ChangeDisplaySettingsA';

Procedure SetWindowTitle(Handle:Cardinal; Name:AnsiString);
Begin
  Windows.SetWindowText(Handle, PAnsiChar(Name));
End;

Procedure DisplayMessage(S:AnsiString);
Begin
  Windows.MessageBoxA(0, PAnsiChar(S), PAnsiChar(GetProgramName), MB_OK Or MB_ICONERROR);
End;

Function GetTime:Cardinal;  {$IFDEF FPC}Inline;{$ENDIF}
Begin
  Result := GetTickCount();
  //Result := {$IFDEF FPC}GetTickCount(){$ELSE}timeGetTime(){$ENDIF};
End;

Function GetCurrentTime:TERRATime;
var
  SystemTime: TSystemTime;
Begin
  GetLocalTime(SystemTime);
  Result.Hour:=SystemTime.wHour;
  Result.Minute:=SystemTime.wMinute;
  Result.Second:=SystemTime.wSecond;
  Result.MiliSecond:=SystemTime.wMilliseconds;
End;

Function GetCurrentDate:TERRADate;
var
  SystemTime: TSystemTime;
Begin
  GetLocalTime(SystemTime);
  Result.Year:=SystemTime.wYear;
  Result.Month:=SystemTime.wMonth;
  Result.Day:=SystemTime.wDay;
  Result.WeekDay:=SystemTime.wDayOfWeek;
End;

Function CreateApplicationClass(Client:AppClient):Application;
Begin
  Result := WindowsApplication.Create(Client);
End;

Function WndProc(hWnd:HWND;Msg:UINT;wParam:wPARAM;lParam:LPARAM):LRESULT;Stdcall;
Var
  Delta:Integer;
  P:MouseCursor;
  I, X,Y:Integer;
  S:AnsiString;
  App:WindowsApplication;
Begin
  Result:=0;

  App := WindowsApplication(Application.Instance);
  If (Not Assigned(App)) Then
  Begin
    Result := DefWindowProc(hWnd,Msg,wParam,lParam);
    Exit;
  End;

  If {((Msg=WM_SYSCOMMAND) And (wParam = SC_MAXIMIZE))
  Or }((Msg=WM_SYSKEYDOWN) And (wParam = keyEnter)) Then
  Begin
    App._ChangeToFullScreen := True;
    Exit;
  End;

  If (Msg=WM_SYSKEYDOWN) And (wParam = (VK_MENU)) Then
  Begin
    Result := 0;
    Exit;
  End;

  Case (Msg) Of
    MM_MCINOTIFY:
      Begin     
        MusicManager.Instance.mciNotifyEvent(wParam, lParam);
      End;

    WM_SYSCOMMAND:
      Begin
        If (wParam = SC_MINIMIZE) Then
        Begin
          App._State := wsMinimized;
          If (Assigned(App._Client)) Then
            App.Client.OnStateChange(App._State);
        End Else
        If (wParam = SC_RESTORE) Then
        Begin
          App._State := wsNormal;
          If (Assigned(App._Client)) Then
            App.Client.OnStateChange(App._State);
        End;

        Result := DefWindowProc(hWnd,Msg,wParam,lParam);
      End;

    WM_SIZE:  Begin
                P := PCursor(@lParam)^;
                App.AddCoordEvent(eventWindowResize, P.X, P.Y, 0);
              End;

    WM_CLOSE: Begin
                PostQuitMessage(0);
                App._Running:=False;
              End;

    WM_KEYDOWN: If (App._CanReceiveEvents) Then
                Begin // Set the pressed key (wparam) to equal true so we can check if its pressed
                  App.AddValueEvent(eventKeyDown, wParam);
                End;

    WM_KEYUP: If (App._CanReceiveEvents) Then
              Begin // Set the released key (wparam) to equal false so we can check if its pressed
                App.AddValueEvent(eventKeyUp, wParam);
              End;

    WM_CHAR:  If (App._CanReceiveEvents) And (Assigned(App._Client)) Then
              Begin
                If (wParam=22) And  ($8000 And GetKeyState(VK_CONTROL)<>0) Then
                Begin
                  S := WindowsApplication(App).GetClipboard();
                  For I:=1 To Length(S) Do
                    App.AddValueEvent(eventKeyPress, Ord(S[I]));
                End Else
                  App.AddValueEvent(eventKeyPress, wParam);
              End;

    WM_LBUTTONDOWN: If (App._CanReceiveEvents) And (Assigned(App._Client)) Then
                    Begin
                      SetCapture(App._Handle);
                      App.AddValueEvent(eventMouseDown, keyMouseLeft);
                    End;

    WM_RBUTTONDOWN: If (App._CanReceiveEvents) And (Assigned(App._Client)) Then
                    Begin
                      App.AddValueEvent(eventMouseDown, keyMouseRight);
                    End;

    WM_MBUTTONDOWN: If (App._CanReceiveEvents) And (Assigned(App._Client)) Then
                    Begin
                      App.AddValueEvent(eventMouseDown, keyMouseMiddle);
                    End;

    WM_RBUTTONUP: If (App._CanReceiveEvents) And (Assigned(App._Client)) Then
                  Begin
                    App.AddValueEvent(eventMouseUp, keyMouseRight);
                  End;

    WM_LBUTTONUP: If (App._CanReceiveEvents) And (Assigned(App._Client)) Then
                  Begin
                    ReleaseCapture;
                    App.AddValueEvent(eventMouseUp, keyMouseLeft);
                  End;

    WM_MBUTTONUP: If (App._CanReceiveEvents) And (Assigned(App._Client)) Then
                  Begin
                    App.AddValueEvent(eventMouseUp, keyMouseMiddle);
                  End;

    WM_MOUSEMOVE: If (App._CanReceiveEvents) And (Assigned(App._Client)) Then
                  Begin
                    P := PCursor(@lParam)^;
                    App.AddCoordEvent(eventMouseMove, P.X, P.Y, 0);

                    If (App._CursorVisible) And (Not App._IgnoreCursor) Then
                    Begin
                      App._CursorVisible := False;
                      ShowCursor(False);
                    End;

                  End;

    WM_NCMOUSEMOVE: Begin
                      If (Not App._CursorVisible) And (Not App._IgnoreCursor)  Then
                      Begin
                        App._CursorVisible := True;
                        ShowCursor(True);
                      End;
                    End;

    WM_MOUSEWHEEL:If (App._CanReceiveEvents) And (Assigned(App._Client)) Then
                  Begin
                    Delta := Integer(wParam Div High(Word));
                    App.AddValueEvent(eventMouseWheel, Delta);
                  End;

    WM_KILLFOCUS: If (App._CanReceiveEvents) Then
                  Begin
                    FillChar(App.Input.Keys[0],255,False);
                    //App.OnDeactivate;
                  End;

    Else
      Begin
        Result := DefWindowProc(hWnd,Msg,wParam,lParam);  // Default result if nothing happens
      End;
  End;
End;

Function WindowsApplication.GetClipboard():AnsiString;
Var
  pText:THandle;
Begin
  OpenClipboard(0);
  pText := GetClipboardData(CF_TEXT);
  CloseClipboard();
  Result := PAnsiChar(GlobalLock(pText));
End;

Procedure WindowsErrorCallback(Msg:AnsiString); Cdecl;
Begin
  MessageBoxA(0, PAnsiChar(Msg), 'Fatal error!', MB_OK);
  Application.Instance.Terminate();
End;

//  Creates the window
Function WindowsApplication.InitWindow:Boolean;
Var
  I:Integer;
  wndClass:TWndClass;         // Window class
  dwStyle:Cardinal;            // Window styles
  dwExStyle:Cardinal;          // Extended window styles
  Inst:HINST;             // Current instance
  X,Y,BW,BH:Integer;
  Size:Cardinal;
  Buf:Array[0..1023] Of AnsiChar;
  joyInfo:GamepadInfoEx;      // extended information
  dwResult:Cardinal;
Begin
  Result := False;

  FatalErrorHandler := WindowsErrorCallback;

  If (_TempPath = '') Then
  Begin
    GetTempPath(1024, @(Buf[0]));
    _TempPath := Buf;
    SetLength(_TempPath, Pred(Length(_TempPath)));
  End;

  _DocumentPath := GetDocumentPath();
  _StoragePath := _DocumentPath;

  If (_CurrentUser = '') Then
  Begin
    GetUserName(@(Buf[0]), Size);
    _CurrentUser := Buf;
  End;

  Inst := GetModuleHandle(Nil);        // Grab an instance for our window

  If Not _MultiSampleInitialized Then
  Begin
    FillChar(WndClass,SizeOf(wndClass),0); // Clear the window class structure

    With wndClass Do                    // Set up the window class
    Begin
     Style:=CS_HREDRAW Or  // Redraws entire window if length changes
            CS_VREDRAW Or  // Redraws entire window if height changes
            CS_OWNDC;      // Unique device context for the window
     lpfnWndProc:=@WndProc;  // Set the window procedure to our func WndProc
     hInstance:=Inst;
     hCursor:=LoadCursor(0,IDC_ARROW);
     hIcon:=LoadIcon(Inst,'IDI_MAIN_ICON');
     lpszClassName:='TERRA';
    End;

    If (RegisterClass(wndClass)=0) Then  // Attemp to register the window class
    Begin
      RaiseError('Failed to register the window class.');
      Exit;
    End;
  End;

  _FullscreenWidth := GetSystemMetrics(SM_CXSCREEN);
  _FullscreenHeight := GetSystemMetrics(SM_CYSCREEN);

  If (_FullScreen) Then
  Begin
    dwExStyle := WS_EX_APPWINDOW;
    dwStyle := WS_POPUP;
    X:=0;
    Y:=0;
    BW:=0;
    BH:=0;
  End Else
  Begin
    dwExStyle := WS_EX_OVERLAPPEDWINDOW{ Or WS_EX_COMPOSITED};
    dwStyle := WS_OVERLAPPED Or WS_MINIMIZEBOX Or WS_MAXIMIZEBOX Or WS_SIZEBOX Or WS_CAPTION Or WS_SYSMENU;
//    dwStyle := WS_POPUP Or WS_BORDER;
    X := (_FullscreenWidth - Width) Div 2;
    Y := (_FullscreenHeight - Height) Div 2;
    Y:=0;

    //BW := GetSystemMetrics(SM_CXFIXEDFRAME)+GetSystemMetrics(SM_CXEDGE)*2;
    BH := GetSystemMetrics(SM_CYCAPTION)+GetSystemMetrics(SM_CYSIZEFRAME)*2+GetSystemMetrics(SM_CYEDGE)*2;
    BW := GetSystemMetrics(SM_CYSIZEFRAME)*2+GetSystemMetrics(SM_CYEDGE)*2;
//    BH := GetSystemMetrics(SM_CYSIZEFRAME)*2;
  End;

  _BorderWidth := BW;
  _BorderHeight := BH;

  _OriginalWidth := _Width;
  _OriginalHeight := _Height;

  _PosX := X;
  _PosY := Y;

  _CanReceiveEvents:=True;

  // Attempt to create the actual window
  _Handle := CreateWindowExA(dwExStyle,    // Extended window styles
                          'TERRA',       // Class name
                          PAnsiChar(Title), // Window title (caption)
                          dwStyle,      // Window styles
                          X,Y,          // Window position
                          Width+BW,     // Size of window
                          Height+BH,
                          HWND_DESKTOP, // No parent window
                          0,            // No menu
                          Inst ,    // Instance
                          Nil);         // Pass nothing to WM_CREATE

  Assert(_Handle<>0,'Unable to create window.');

  // Initialize joysticks
  For I:=0 To 3 Do
  Begin
    If XIsControllerConnected(I) Then
      AddGamePad(I, True);

    dwResult := joyGetPosEx(I, joyInfo);
    If (dwResult = JOYERR_NOERROR) Then
      AddGamePad(I, False);
      //joyGetDevCaps(0, @_JoyCaps, SizeOf(_JoyCaps));
  End;

  // Detect system language
  Size := GetLocaleInfo (LOCALE_USER_DEFAULT, LOCALE_SABBREVLANGNAME, Nil, 0);
  SetLength(_Language, Succ(Size));
  GetLocaleInfo(LOCALE_USER_DEFAULT, LOCALE_SABBREVLANGNAME, @(_Language[1]), Size);
  SetLength(_Language, 2);
  _Language := UpStr(_Language);

  //_Width := 600;
  //_Height := 1136;
  //SetWindowPos(_Handle, 0, 200, 200, _Width, _Height, $400);

  _CanReceiveEvents := False;
  _Ready := True;
  Result := True;
End;

Function WindowsApplication.InitGraphics:Boolean;
Var
  Pfd:PixelFormatDescriptor; // Settings for the OpenGL window
Begin
  Result:=False;

  _HDC := GetDC(_Handle);
  If _HDC=0 Then
  Begin
    RaiseError('Unable to retrieve a device context.');
    Halt;
  End;
   
 // Settings for the OpenGL window
  FillChar(Pfd,SizeOf(Pfd),0);
  With Pfd Do
  Begin
    nSize := SizeOf(PixelFormatDescriptor); // Size Of This Pixel Format Descriptor
    nVersion := 1;                          // The version of this data structure
    dwFlags := PFD_DRAW_TO_WINDOW Or PFD_SUPPORT_OPENGL Or PFD_DOUBLEBUFFER;
    iPixelType := PFD_TYPE_RGBA;            // RGBA color format
    cColorBits := 24;                       // OpenGL color depth
    cAlphaBits := 8;                        //
    cDepthBits := 24;                       // Specifies the depth of the depth buffer
    cStencilBits := 8;                      // Specificies the depth of the stencil buffer
    iLayerType := PFD_MAIN_PLANE;
  End;

  // Attempts to find the pixel format supported by a device context that is the best match to a given pixel format specification.
  If (_MultiSampleInitialized) And (_MultisampleFormat<>0) Then
    _PixelFormat := _MultisampleFormat
  Else
    _PixelFormat := ChoosePixelFormat(_hDC,@Pfd);

  If (_PixelFormat=0) Then
  Begin
    RaiseError('Unable to find a suitable pixel format.');
    Exit;
  End;

  // Sets the specified device context's pixel format to the format specified by the PixelFormat.
  If (Not SetPixelFormat(_HDC, _PixelFormat,@Pfd)) then
  Begin
    RaiseError('Unable to set the pixel format.');
    Exit;
  End;

  // Create a OpenGL rendering context
  _hRC := wglCreateContext(_hDC);
  If (_hRC = 0) Then
  Begin
    RaiseError('Unable to create an OpenGL rendering context.');
    Exit;
  End;

  // Makes the specified OpenGL rendering context the calling thread's current rendering context
  If (Not wglMakeCurrent(_hDC,_hRC))Then
  Begin
    RaiseError('Unable to activate OpenGL rendering context.');
    Exit;
  End;

  If (Not _MultiSampleInitialized) And (Not _Managed) Then
  Begin
    _MultiSampleInitialized := True;
    _MultisampleFormat := InitMultisample(Self._Handle, pfd, _HDC);
    If _MultisampleFormat<>0 Then
    Begin
      //glCoverage := GL_SAMPLE_ALPHA_TO_COVERAGE;
      DestroyWindow(_Handle);
      Self.InitWindow;
      Result := Self.InitGraphics;
      Exit;
    End;
  End;

  // Settings to ensure that the window is the topmost window
  If Not _Hidden Then
  Begin
    UpdateWindow(_Handle);
    ShowWindow(_Handle,SW_SHOW);
    SetForegroundWindow(_Handle);
    SetFocus(_Handle);

    If (Not _IgnoreCursor) Then
      ShowCursor(False);
  End;

  _CursorVisible := False;
  //ReleaseCapture;
  //SetCapture(_Handle);

  If (_FullScreen) Then
  Begin
    _FullScreen := False;
    ToggleFullScreen;
  End;

  glLoadExtensions();

  If (Not Self.Client.GetVSync()) Then
    wglSwapIntervalEXT(0);  // Disable VSync

  If _MultisampleFormat<>0 Then
    glEnable(GL_MULTISAMPLE);

  Result := True;
End;

// Assign the current thread to one processor. This ensures that timing
// code runs on only one processor, and will not suffer any ill effects
// from power management.
//
// Based on DXUTSetProcessorAffinity() function from the DXUT framework.
{Procedure WindowsApplication.SetProcessorAffinity;
Var
  dwProcessAffinityMask:DWORD;
  dwSystemAffinityMask:DWORD;
  dwAffinityMask:DWORD;
  hCurrentProcess:THANDLE;
  hCurrentThread:THANDLE;
Begin
  dwProcessAffinityMask := 0;
  dwSystemAffinityMask := 0;
  hCurrentProcess := GetCurrentProcess();

  If (Not GetProcessAffinityMask(hCurrentProcess, dwProcessAffinityMask, dwSystemAffinityMask)) Then
    Exit;

  If (dwProcessAffinityMask<>0) Then
  Begin
    // Find the lowest processor that our process is allowed to run against.
    dwAffinityMask := (dwProcessAffinityMask And ((Not dwProcessAffinityMask) + 1));

    // Set this as the processor that our thread must always run against.
    // This must be a subset of the process affinity mask.
    hCurrentThread := GetCurrentThread();
    if (hCurrentThread <> INVALID_HANDLE_VALUE) Then
    Begin
      SetThreadAffinityMask(hCurrentThread, dwAffinityMask);
      //SetThreadIdealProcessor
      CloseHandle(hCurrentThread);
    End;
  End;

  CloseHandle(hCurrentProcess);
End;
}

Procedure WindowsApplication.CloseWindow;
Begin
  If ((_Handle <> 0)And(Not DestroyWindow(_Handle)))Then
  Begin
    RaiseError('Unable to destroy window.');
    _Handle:=0;
  End;

 // Attempts to unregister the window class
  If (Not UnRegisterClass('TERRA',hInstance))Then
  Begin
    RaiseError('Unable to unregister window class.');
  End;
End;

Procedure WindowsApplication.CloseGraphics;
Begin
	If (_Fullscreen)	Then
    ToggleFullScreen;

  If (Not _IgnoreCursor) Then
    ShowCursor(True);
  //ReleaseCapture;

  // Makes current rendering context not current, and releases the device
  // context that is used by the rendering context.
  If (Not wglMakeCurrent(_hDC,0)) Then
  Begin
    RaiseError('Release of DC and RC failed.');
  End;

  // Attempts to delete the rendering context
  If (Not wglDeleteContext(_hRC)) Then
  Begin
    RaiseError('Release of rendering context failed.');
    _hRC:=0;
  End;

  ReleaseDC(_Handle,_HDC);
End;

Procedure WindowsApplication.SwapBuffers;
Begin
 	Windows.SwapBuffers(_hDC);									// Swap Buffers (Double Buffering)
End;

Procedure WindowsApplication.SetState(State:Cardinal);
Begin
  _State := State;
  Case State Of
  wsNormal:    ShowWindow(_Handle,SW_RESTORE);
  wsMinimized: ShowWindow(_Handle,SW_MINIMIZE);
  wsMaximized: ShowWindow(_Handle,SW_MAXIMIZE);
  End;
End;

{$RANGECHECKS OFF}
Procedure WindowsApplication.ToggleFullscreen;
Var
  FullWidth, FullHeight:Integer;
  ScreenSettings:DevMode;
Begin
  _Fullscreen := Not _FullScreen;

  If (_FullScreen) Then
  Begin
    // Moving to full screen mode.
    _savedExStyle := GetWindowLong(_Handle, GWL_EXSTYLE);
    _savedStyle := GetWindowLong(_Handle, GWL_STYLE);
    GetWindowRect(_Handle, _rcSaved);

    //FullWidth := _OriginalWidth;
    //FullHeight := _OriginalHeight;
    FullWidth := 1024;
    FullHeight := 768;

    ZeroMemory(@ScreenSettings,SizeOf(ScreenSettings));
    With ScreenSettings Do
    Begin   // Set parameters for the screen setting
     dmSize:=SizeOf(ScreenSettings);
     dmPelsWidth:= FullWidth;     // Window width
     dmPelsHeight:= FullHeight;   // Window height
     dmBitsPerPel:= 32;      // Window color depth
     dmFields:=DM_PELSWIDTH Or DM_PELSHEIGHT Or DM_BITSPERPEL;
    End;

    // Try to change screen mode to fullscreen
    If (ChangeDisplaySettings(@ScreenSettings,CDS_FULLSCREEN)=DISP_CHANGE_FAILED)Then
    Begin
      RaiseError('Unable to switch to fullscreen.');
     _Fullscreen:=False;
      Exit;
    End;

    SetWindowLong(_Handle, GWL_EXSTYLE, 0);
    SetWindowLong(_Handle, GWL_STYLE, WS_POPUP Or WS_CLIPCHILDREN Or WS_CLIPSIBLINGS);
    SetWindowPos(_Handle, HWND_TOPMOST, 0, 0, FullWidth, FullHeight, SWP_FRAMECHANGED Or SWP_SHOWWINDOW);
  End Else
  Begin
    ChangeDisplaySettings(Nil, 0);

    // Moving back to windowed mode.
    SetWindowLong(_Handle, GWL_EXSTYLE, _savedExStyle);
    SetWindowLong(_Handle, GWL_STYLE, _savedStyle);
    SetWindowPos(_Handle, HWND_NOTOPMOST, _rcSaved.left, _rcSaved.top, _rcSaved.Right - _rcSaved.Left, _rcSaved.Bottom - _rcSaved.top, SWP_SHOWWINDOW);
  End;
End;

Procedure WindowsApplication.ProcessMessages;
Var
  I, PlayerID:Integer;
  Msg:TMsg;
  JoyInfo:GamepadInfoEx;
  XState:TXInputGamepad;
Begin
  If (PeekMessage(Msg, _Handle, 0, 0, PM_REMOVE)) Then // Check if there is a message for this window
  Begin
    If (Msg.Message=WM_QUIT) Then     // If WM_QUIT message received then we are done
    Begin
      _Running:=False;
      Exit;
    End Else
    Begin   // Else translate and dispatch the message to this window
      TranslateMessage(msg);
      DispatchMessage(msg);
    End;
  End;

  If (_GamePadCount>0) And (GetTime>_NextJoystickUpdate) Then
  Begin
    _NextJoystickUpdate := GetTime+50;
    For I:=0 To Pred(_GamepadCount) Do
    If (_Gamepads[I].XInput) Then
    Begin
      PlayerID := _Gamepads[I].LocalID;
      XState := XGetControllerState();
      SetKeyPress(keyGamepadCount * PlayerID + keyGamePadUp, (XState.Buttons And XINPUT_GAMEPAD_DPAD_UP)<>0);
      SetKeyPress(keyGamepadCount * PlayerID + keyGamePadDown, (XState.Buttons And XINPUT_GAMEPAD_DPAD_DOWN)<>0);
      SetKeyPress(keyGamepadCount * PlayerID + keyGamePadLeft, (XState.Buttons And XINPUT_GAMEPAD_DPAD_LEFT)<>0);
      SetKeyPress(keyGamepadCount * PlayerID + keyGamePadRight, (XState.Buttons And XINPUT_GAMEPAD_DPAD_RIGHT)<>0);

      SetKeyPress(keyGamepadCount * PlayerID + keyGamePadMenu, (XState.Buttons And XINPUT_GAMEPAD_START)<>0);

      SetKeyPress(keyGamepadCount * PlayerID + keyGamePadA, (XState.Buttons And XINPUT_GAMEPAD_A)<>0);
      SetKeyPress(keyGamepadCount * PlayerID + keyGamePadB, (XState.Buttons And XINPUT_GAMEPAD_B)<>0);
      SetKeyPress(keyGamepadCount * PlayerID + keyGamePadX, (XState.Buttons And XINPUT_GAMEPAD_X)<>0);
      SetKeyPress(keyGamepadCount * PlayerID + keyGamePadY, (XState.Buttons And XINPUT_GAMEPAD_Y)<>0);

      SetKeyPress(keyGamepadCount * PlayerID + keyGamePadL, (XState.Buttons And XINPUT_GAMEPAD_LEFT_SHOULDER)<>0);
      SetKeyPress(keyGamepadCount * PlayerID + keyGamePadR, (XState.Buttons And XINPUT_GAMEPAD_RIGHT_SHOULDER)<>0);
    End Else
    Begin
      PlayerID := _Gamepads[I].LocalID;
      JoyInfo.dwSize := SizeOf(JoyInfo);
      JoyInfo.dwFlags := JOY_RETURNALL;
      joyGetPosEx(0, JoyInfo);

      //Windows.SetWindowText(Handle, PAnsiChar(IntToString(JoyInfo.wXpos)));

      SetKeyPress(keyGamepadCount * PlayerID + keyGamePadUp, (JoyInfo.wYpos=0));
      SetKeyPress(keyGamepadCount * PlayerID + keyGamePadDown, (JoyInfo.wYpos=65535));
      SetKeyPress(keyGamepadCount * PlayerID + keyGamePadLeft, (JoyInfo.wXpos=0));
      SetKeyPress(keyGamepadCount * PlayerID + keyGamePadRight, (JoyInfo.wXpos=65535));

      SetKeyPress(keyGamepadCount * PlayerID + keyGamePadA, (JoyInfo.wButtons And $2<>0));
      SetKeyPress(keyGamepadCount * PlayerID + keyGamePadB, (JoyInfo.wButtons And $4<>0));
      SetKeyPress(keyGamepadCount * PlayerID + keyGamePadX, (JoyInfo.wButtons And $8<>0));
      SetKeyPress(keyGamepadCount * PlayerID + keyGamePadY, (JoyInfo.wButtons And $1<>0));

      SetKeyPress(keyGamepadCount * PlayerID + keyGamePadL, (JoyInfo.wButtons And $10<>0));
      SetKeyPress(keyGamepadCount * PlayerID + keyGamePadR, (JoyInfo.wButtons And $20<>0));
      SetKeyPress(keyGamepadCount * PlayerID + keyGamePadD, (JoyInfo.wButtons And $40<>0));
      SetKeyPress(keyGamepadCount * PlayerID + keyGamePadZ, (JoyInfo.wButtons And $80<>0));
    End;
  End;
End;

Procedure WindowsApplication.Yeld;
Begin
  Sleep(0);
End;

Function ShellExecute(hWnd: HWND; Operation, FileName, Parameters, Directory: PAnsiChar; ShowCmd: Integer): HINST; stdcall; external 'shell32.dll' name 'ShellExecuteA';

Procedure WindowsApplication.OpenAppStore(URL:AnsiString);
Var
  S:PAnsiChar;
Begin
  If (Pos('http',URL)>0) Or (Pos('file',URL)>0) Then
    S := PAnsiChar(URL)
  Else
    S := PAnsiChar('http://itunes.apple.com/us/app/myapp/id'+URL);
  ShellExecute(Application.Instance.Handle, 'open', S,nil,nil, SW_SHOWNORMAL) ;
End;

Function WindowsApplication.IsDebuggerPresent: Boolean;
type
  TDebugProc = function : boolean;
    stdcall;
var
  Kernel32: HMODULE;
  DebugProc: TDebugProc;
begin { DebuggerPresent }
  Kernel32 := GetModuleHandle('kernel32');
  if Kernel32<>0 then
  begin
    DebugProc := TDebugProc(GetProcAddress(Kernel32, 'IsDebuggerPresent'));
    if Assigned(DebugProc) then
      Result := DebugProc();
  end; { Kernel32<>0 }
end;

Function GetMACAdress():AnsiString;
var
  NCB: PNCB;
  Adapter: PAdapterStatus;

  URetCode: PAnsiChar;
  RetCode: Char;
  I: integer;
  Lenum: PlanaEnum;
  _SystemID:AnsiString;
  TMPSTR:AnsiString;
begin
  Result    := '';
  _SystemID := '';
  Getmem(NCB, SizeOf(TNCB));
  Fillchar(NCB^, SizeOf(TNCB), 0);

  Getmem(Lenum, SizeOf(TLanaEnum));
  Fillchar(Lenum^, SizeOf(TLanaEnum), 0);

  Getmem(Adapter, SizeOf(TAdapterStatus));
  Fillchar(Adapter^, SizeOf(TAdapterStatus), 0);

  Lenum.Length    := chr(0);
  NCB.ncb_command := chr(NCBENUM);
  NCB.ncb_buffer  := Pointer(Lenum);
  NCB.ncb_length  := SizeOf(Lenum);
  RetCode         := Netbios(NCB);

  i := 0;
  repeat
    Fillchar(NCB^, SizeOf(TNCB), 0);
    Ncb.ncb_command  := chr(NCBRESET);
    Ncb.ncb_lana_num := lenum.lana[I];
    RetCode          := Netbios(Ncb);

    Fillchar(NCB^, SizeOf(TNCB), 0);
    Ncb.ncb_command  := chr(NCBASTAT);
    Ncb.ncb_lana_num := lenum.lana[I];
    // Must be 16
    Ncb.ncb_callname := '*               ';

    Ncb.ncb_buffer := Pointer(Adapter);

    Ncb.ncb_length := SizeOf(TAdapterStatus);
    RetCode        := Netbios(Ncb);
    //---- calc _systemId from mac-address[2-5] XOR mac-address[1]...
    if (RetCode = chr(0)) or (RetCode = chr(6)) then
    begin
      _SystemId := HexStr(Ord(Adapter.adapter_address[0])) + '-' +
        HexStr(Ord(Adapter.adapter_address[1])) + '-' +
        HexStr(Ord(Adapter.adapter_address[2])) + '-' +
        HexStr(Ord(Adapter.adapter_address[3])) + '-' +
        HexStr(Ord(Adapter.adapter_address[4])) + '-' +
        HexStr(Ord(Adapter.adapter_address[5]));
    end;
    Inc(i);
  until (I >= Ord(Lenum.Length)) or (_SystemID <> '00-00-00-00-00-00');
  FreeMem(NCB);
  FreeMem(Adapter);
  FreeMem(Lenum);
  GetMacAdress := _SystemID;
End;

Function WindowsApplication.GetDeviceID:AnsiString;
Begin
  Result := GetMACAdress();
End;

Function WindowsApplication.GetControllerCount: Integer;
Begin
  Result := Self._GamepadCount;
//  Result := 1;
End;

Function WindowsApplication.AddGamePad(DeviceID:Integer; XInput:Boolean):Boolean;
Begin
  If (_GamePadCount>=4) Then
  Begin
    Result := False;
    Exit;
  End;

  Inc(_GamepadCount);
  SetLength(_Gamepads, _GamepadCount);
  _Gamepads[Pred(_GamepadCount)].DeviceID := DeviceID;
  _Gamepads[Pred(_GamepadCount)].LocalID := Pred(_GamepadCount);
  _Gamepads[Pred(_GamepadCount)].XInput := XInput;
  Result := True;
End;

Procedure WindowsApplication.SendEmail(DestEmail, Subject, Body:AnsiString);
Begin

End;

Function WindowsApplication.SetOrientation(Value:Integer):Boolean;
Var
  Temp:Boolean;
  Rect:TRect;
Begin
  Temp := (IsLandscapeOrientation(Self.Orientation));

  Result := Inherited SetOrientation(Value);

  If Not Result Then
    Exit;

  If ((IsLandscapeOrientation(Self.Orientation)) <> Temp) Then
  Begin
    GetWindowRect(_Handle, Rect);
    SetWindowPos(_Handle, 0, Rect.Left, Rect.Top, _Height, _Width, $400);
  End;
End;

Procedure WindowsApplication.DisableAds;
Begin
  Self.SetViewport(0, 0, Application.Instance.Width, Application.Instance.Height);
End;

Procedure WindowsApplication.EnableAds;
Begin
  Self.SetViewport(0, 50, Application.Instance.Width, Application.Instance.Height);
  //Self.SetViewport(0, 0, Application.Instance.Width, Application.Instance.Height-50);
End;

Function WindowsApplication.GetDocumentPath:AnsiString;
Begin
  Result := GetCurrentDir() + PathSeparator + 'Data';
  If Not DirectoryExists(Result) Then
  Begin
    CreateDir(Result);
  End;
End;

Initialization
  LoadMultimedia();
End.
